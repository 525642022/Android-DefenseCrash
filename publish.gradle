apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

//./gradlew clean ./gradlew build ./gradlew bintrayUpload


group = project.GROUP_ID
version = project.VERSION


publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/$project.name-release.aar")
            groupId project.GROUP_ID
            artifactId project.ARTIFACT_ID
            version project.VERSION

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    def bintrayUser = bintrayUser
    def bintrayApiKey = bintrayApiKey

    user = bintrayUser
    key = bintrayApiKey
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = "maven"
        name = project.NAME
        licenses = ["Apache-2.0"]
        publish = true
        description = "java crash handler for androiod"
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/xuuhaoo/Android-DefenseCrash'
        dryRun = false
        version {
            name = project.VERSION
            desc = "java crash handler for androiod"
            released = new Date()
            vcsTag = project.VERSION
        }
    }
}
